# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2
pkgver=2.0.9
pkgrel=1
pkgdesc="A library for portable low-level access to a video framebuffer, audio output, mouse, and keyboard (Nintendo WiiU port)"
arch=('any')
url="https://libsdl.org"
license=("LGPL")
options=(!strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=('wut')
source=("${url}/release/SDL2-${pkgver}.tar.gz" "SDL2-${pkgver}.patch")
groups=('wiiu-portlibs' 'wiiu-sdl2-libs')
sha256sums=(
  '255186dc676ecd0c1dbf10ec8a2cc5d6869b5079d8a38194c2aecdff54b324b1'
  '1cb49326a54fbdc1a8b76654a29a113bb14464892571c7f3a38dcc0d2931d35b'
)

build() {
  cd SDL2-$pkgver
  patch -p1 -i $srcdir/SDL2-${pkgver}.patch
  mkdir build
  cd build
  cmake .. \
  -DCMAKE_TOOLCHAIN_FILE=/opt/devkitpro/wut/share/wut.toolchain.cmake \
  -DCMAKE_BUILD_TYPE="Release" \
  -DCMAKE_INSTALL_PREFIX=/opt/devkitpro/portlibs/wiiu
  make
}

package() {

  cd SDL2-$pkgver/build
  make install DESTDIR=$pkgdir

}
