# Maintainer: Ash Logan <ash [at] heyquark [dot] com>
# Maintainer: WinterMute <davem@devkitpro.org>
pkgname=wut-tools
pkgver=1.1.1
pkgrel=1
pkgdesc='Tools for Nintendo WiiU homebrew development)'
arch=('any')
url="https://github.com/devkitpro/${pkgname}"
license=('GPLv2')
options=(!strip libtool staticlibs)
source=("${url}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.xz")
sha256sums=('4f3bfc20f2c7c68cf4b91b0d11ceb3e7985939e23f72d0f633ef90f6d0a100de')
groups=('wiiu-dev')

if [[ "$CHOST" == *"-w64-mingw32" ]]; then
  depends=('tool-dlls')
fi

crossbuild() {
  if [ -n "$CHOST" ]; then

    source ~/cross-build-$CHOST.sh
    CONFIGURE_FLAGS="--host=$CHOST"

  fi
}

build() {

  cd $pkgname-$pkgver

  crossbuild

  ./configure $CONFIGURE_FLAGS --prefix=/opt/devkitpro/tools

  make

}

package() {

  cd $pkgname-$pkgver

  crossbuild

  make DESTDIR="$pkgdir" install-strip

  install -Dm644 LICENSE.md "$pkgdir"/opt/devkitpro/licenses/$pkgname/COPYING
  
}
