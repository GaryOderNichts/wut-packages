# Maintainer:  Dave Murphy <davem@devkitpro.org>

pkgname=wiiu-sdl2_image
pkgver=2.0.4
pkgrel=1
pkgdesc="SDL2 image loading library (Nintendo WiiU port)."
arch=('any')
url="https://libsdl.org/projects/SDL_image/"
license=("zlib")
options=(!strip libtool staticlibs)
makedepends=('wiiu-pkg-config' 'devkitpro-pkgbuild-helpers')
depends=(
  'wiiu-sdl2'
  'ppc-libpng'
  'ppc-libjpeg-turbo'
)
source=("${url}release/SDL2_image-${pkgver}.tar.gz")
sha256sums=(
  'e74ec49c2402eb242fbfa16f2f43a19582a74c2eabfbfb873f00d4250038ceac'
)
groups=('wiiu-portlibs' 'wiiu-sdl2-libs')

build() {
  cd SDL2_image-$pkgver

  source ${DEVKITPRO}/wiiuvars.sh

  sed '/^noinst_PROGRAMS/d' -i Makefile.in

  ./configure --prefix="${PORTLIBS_PREFIX}" \
    --host=powerpc-eabi --disable-shared --enable-static \
    --disable-sdltest \
    --with-sdl-prefix=${PORTLIBS_PREFIX}

  make
}

package() {
  cd SDL2_image-$pkgver

  source /opt/devkitpro/wiiuvars.sh

  make DESTDIR="$pkgdir" install

  # add our static libs
  echo "Requires.private: libpng libjpeg" >> "${pkgdir}/${PORTLIBS_PREFIX}/lib/pkgconfig/SDL2_image.pc"

  # License
  install -Dm644 "COPYING.txt" "${pkgdir}/${PORTLIBS_PREFIX}/licenses/${pkgname}/COPYING.txt"
}
